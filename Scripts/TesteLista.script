// Script:    TesteLista.script
// Autor:     Luiz Carlos Rumbelsperger Viana
// Data:      2/out/2018
// Propósito: Este script testa de forma completa, todos os tipos de retorno para cada funcao
// de acesso da interface lista.h. O tipo de valor da lista eh generico, esse script ira
// testar com valores tipo string.
//

== Inicializar valores
=declararparm   lista0  int 0
=declararparm   lista1  int 1
=declararparm   lista2  int 2

=declararparm   str0    string  "abcdefg"
=declararparm   str1    string  "qwerty"
=declararparm   str2    string  "xxx"
=declararparm   str3    string  "xpto"

=declararparm   OK             int  0
=declararparm   ListaVazia     int  1
=declararparm   FimLista       int  2
=declararparm   NaoAchou       int  3
=declararparm   FaltouMemoria  int  4 //nao sera testado

=declararparm   pNULL          int  0
=declararparm   pNaoNULL       int  1

==Inicializar contexto
=resetteste

== Criar a lista 0
=criarlista      lista0

== Verificar ObterValor relativo a lista vazia
=obtervalorelem  lista0   "???"     pNULL

== Excluir elemento de lista vazia
=excluirelem     lista0             ListaVazia

== Avancar elemento em lista vazia
=avancarelem     lista0   1    ListaVazia

== Inserir elemento em lista vazia
=inselemantes    lista0   str0   OK
=obtervalorelem  lista0   str0   pNaoNULL

== Chegar ao final da lista e voltar ao inicio
=avancarelem     lista0   1   FimLista
=irinicio	 lista0

== Inserir elemento após primeiro
=inselemapos     lista0   str1   OK
=obtervalorelem  lista0   str1   pNaoNULL

== Ir ao inicio, avancar ao final, voltar ao inicio, ir ao final
=irinicio	 lista0
=avancarelem     lista0   1   OK
=irinicio	 lista0
=irfinal	 lista0

== Inserir elemento antes do último
=inselemantes    lista0   str2   OK
=obtervalorelem  lista0   str2   pNaoNULL

== Excluir elemento do meio
=obtervalorelem  lista0   str2   pNaoNULL
=excluirelem     lista0             OK

== Excluir elemento do início
=obtervalorelem  lista0   str0   pNaoNULL
=excluirelem     lista0             OK

== Excluir elemento final, único 
=obtervalorelem  lista0   str1   pNaoNULL
=excluirelem     lista0             OK

== Ver se a lista ficou vazia
=obtervalorelem  lista0   "???"     pNULL

== Destruir a lista
=destruirlista   lista0

== Criar uma lista com dois elementos
=criarlista      lista1 
=inselemapos     lista1   str0   OK
=inselemapos     lista1   str1   OK

== Esvaziar a lista e ver se ficou vazia
=esvaziarlista   lista1
=obtervalorelem  lista1   "???"     pNULL

== Destruir a lista
=destruirlista   lista1

== Criar uma lista com quatro elementos
=criarlista      lista2 
=inselemapos     lista2   str0   OK
=inselemapos     lista2   str1   OK
=inselemapos     lista2   str2   OK
=inselemapos     lista2   str3   OK

== Verificar a lista de trás para diante
=obtervalorelem  lista2   str3   pNaoNULL
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   str2   pNaoNULL
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   str1   pNaoNULL
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   str0   pNaoNULL
=avancarelem     lista2   -1     FimLista

== Verificar a lista de frente para trás
=obtervalorelem  lista2   str0   pNaoNULL
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   str1   pNaoNULL
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   str2   pNaoNULL
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   str3   pNaoNULL
=avancarelem     lista2   1      FimLista

== Verificar a lista andando demais de trás para diante
=avancarelem     lista2   -10     FimLista
=obtervalorelem  lista2   str0   pNaoNULL

== Verificar a lista andando demais de frente para trás
=avancarelem     lista2   10     FimLista
=obtervalorelem  lista2   str3   pNaoNULL

== Destruir a lista
=destruirlista   lista2

// fim
